# Use Modern User Interface style
!include "MUI.nsh"

!include Library.nsh
!include Sections.nsh

# Select the compressor that will compress the installer
SetCompressor bzip2

# The name of the installer
Name "PyTables Pro @VERSION@"

# The file where the installer will be saved
OutFile "PyTablesPro-@VERSION@.win32-py@DOTPYTHONVERSION@.exe"

# The commands Icon/UninstallIcon with MUI Instead we have to use
# the following !define (see NSIS FAQ)
!define MUI_ICON "@SVN_PTPRO@\mswindows\ptinstaller.ico"
!define MUI_UNICON "@SVN_PTPRO@\mswindows\ptinstaller.ico"

# Use the new XP style
XPStyle on

# Variables declaration
Var PTPRO_SUBKEY
Var STARTMENU_FOLDER
Var MUI_TEMP
Var PYTHONDIR
Var UNCUSTOMTEXT

###########################################################
#	PART I: INSTALLER
###########################################################

# --------------------------------
# Functions
# --------------------------------

Function .onInit
	# The registry key where application settings will be stored
	StrCpy $PTPRO_SUBKEY "SOFTWARE\Carabos\PyTables_Pro\Python@PYTHONVERSION@"
	StrCpy $STARTMENU_FOLDER "PyTables Pro for Python@DOTPYTHONVERSION@"
FunctionEnd

Function preInstallDir
  # Default installation directory
  StrCpy $INSTDIR "$PROGRAMFILES\$STARTMENU_FOLDER"
FunctionEnd

Function prePythonDir
  # Default Python interpreter
  StrCpy $PYTHONDIR "C:\python@PYTHONVERSION@"
FunctionEnd

Function postPythonDir
  IfFileExists $PYTHONDIR\pythonw.exe +3 0
  MessageBox MB_YESNO "Cannot find a valid Python@DOTPYTHONVERSION@ interpreter in \
$PYTHONDIR.$\r$\nYou can install PyTables Pro there, but it will not run \
without Python@DOTPYTHONVERSION@ installed in the same directory tree.$\r$\nContinue \
anyway?" IDYES +2 IDNO 0
  Abort
FunctionEnd

# --------------------------------
# Pages
# --------------------------------

# Welcome page
!insertmacro MUI_PAGE_WELCOME

# License summary page
!define MUI_PAGE_HEADER_TEXT "License Summary"
!define MUI_PAGE_HEADER_SUBTEXT "Just for lazy people :-)"
!define MUI_LICENSEPAGE_TEXT_TOP " "
!define MUI_LICENSEPAGE_TEXT_BOTTOM " "
!define MUI_LICENSEPAGE_BUTTON "I'm warned"
!insertmacro MUI_PAGE_LICENSE @SVN_PTPRO@\LICENSE_SUMMARY.txt

# License page
!insertmacro MUI_PAGE_LICENSE @SVN_PTPRO@\LICENSE.txt

# Components selection page
!insertmacro MUI_PAGE_COMPONENTS

# Installation directory page for PyTables Pro data files
!define MUI_PAGE_CUSTOMFUNCTION_PRE preInstallDir
!define MUI_DIRECTORYPAGE_TEXT_TOP "Setup will install PyTables Pro @VERSION@ data \
files in the following folder. To install in a different folder, click \
Browse and select another folder. Click Next to Continue."
!insertmacro MUI_PAGE_DIRECTORY

# Installation directory page for PyTables Pro modules and scripts
!define MUI_PAGE_CUSTOMFUNCTION_PRE prePythonDir
!define MUI_PAGE_CUSTOMFUNCTION_LEAVE postPythonDir
!define MUI_PAGE_HEADER_TEXT "Choose the Python-@DOTPYTHONVERSION@ interpreter"
!define MUI_PAGE_HEADER_SUBTEXT "Choose the folder in which the Python-@DOTPYTHONVERSION@ \
interpreter is located."
!define MUI_DIRECTORYPAGE_TEXT_TOP "Setup will install PyTables Pro @VERSION@ modules and \
scripts under the following folder. To install in a different folder, click \
Browse and select another folder. Click Next to Continue."
!define MUI_DIRECTORYPAGE_VARIABLE $PYTHONDIR
!insertmacro MUI_PAGE_DIRECTORY


# Start Menu Folder Page Configuration
!define MUI_STARTMENUPAGE_REGISTRY_ROOT "HKLM"
!define MUI_STARTMENUPAGE_REGISTRY_KEY $PTPRO_SUBKEY
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "Start Menu Folder"
!insertmacro MUI_PAGE_STARTMENU Application $STARTMENU_FOLDER

# Install selected components page
!define MUI_FINISHPAGE_CANCEL_ENABLED
!insertmacro MUI_PAGE_INSTFILES

# The stuff to install a.k.a. Sections
# ------------------------------------------

Section "PyTables Pro @PTVERSION@" PT

  # Copy the PyTables Pro utilities in the Python Scripts directory
  SetOutPath $PYTHONDIR\Scripts
  File /x .svn @SVN_PTPRO@\utils\*

  # Copy the PyTables Pro sources in the Python site-packages directory
  SetOutPath $PYTHONDIR\Lib\site-packages\tables
  File /r /x .svn @SVN_PTPRO@\tables\*

  # Copy the PyTables Pro extensions in the tables directory
  SetOutPath $PYTHONDIR\Lib\site-packages\tables
  File @TABLES_ORIG@\*.pyd
  SetOutPath $PYTHONDIR\Lib\site-packages\tables\numexpr
  File @TABLES_ORIG@\numexpr\*.pyd

  # The text files: TODO.txt, INSTALL.txt and stuff like that
  SetOutPath $INSTDIR
  File /x packagingHOWTO.txt @SVN_PTPRO@\*.txt

  # The documentation directory
  SetOutPath $INSTDIR\doc
  File @SVN_PTPRO@\doc\usersguide.pdf
  SetOutPath $INSTDIR\doc\html
  File /r @SVN_PTPRO@\doc\html\*

  # The examples directory
  SetOutPath $INSTDIR\examples
  File @SVN_PTPRO@\examples\*.py
  File @SVN_PTPRO@\examples\check_examples.sh

  # The LICENSES directory
  SetOutPath $INSTDIR\LICENSES
  File /r /x .svn @SVN_PTPRO@\LICENSES\*.txt

  # Create the uninstaller and put data in the aplication directory
  SetOutPath $INSTDIR
  WriteUninstaller "$INSTDIR\Uninstall.exe"

  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application

    # Create shortcuts
    CreateDirectory "$SMPROGRAMS\$STARTMENU_FOLDER"
    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\Examples.lnk" "$INSTDIR\examples"
    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\User's Guide (PDF).lnk" "$INSTDIR\doc\usersguide.pdf"
    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\User's Guide (HTML).lnk" "$INSTDIR\doc\html\index.html"
    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\Uninstall.lnk" "$INSTDIR\Uninstall.exe"

  !insertmacro MUI_STARTMENU_WRITE_END

  # Install needed libraries under the PyTables Pro directory
  SetOutPath $PYTHONDIR\Lib\site-packages\tables
  !insertmacro InstallLib DLL NOTSHARED NOREBOOT_PROTECTED @WINDOWS32@\hdf5dll.dll \
  hdf5dll.dll $PYTHONDIR\Lib\site-packages\tables
  !insertmacro InstallLib DLL NOTSHARED NOREBOOT_PROTECTED @WINDOWS32@\zlib1.dll \
  zlib1.dll $PYTHONDIR\Lib\site-packages\tables
  !insertmacro InstallLib DLL NOTSHARED NOREBOOT_PROTECTED @WINDOWS32@\szlibdll.dll \
  szlibdll.dll $PYTHONDIR\Lib\site-packages\tables
  !insertmacro InstallLib DLL NOTSHARED NOREBOOT_PROTECTED @WINDOWS32@\bzip2.dll \
  bzip2.dll $PYTHONDIR\Lib\site-packages\tables

  # Update the registry
  WriteRegStr HKLM $PTPRO_SUBKEY "INSTDIR" $INSTDIR
  WriteRegStr HKLM $PTPRO_SUBKEY "PYTHONDIR" $PYTHONDIR
  WriteRegStr HKLM $PTPRO_SUBKEY "hdfinst" "1"
  WriteRegStr HKLM $PTPRO_SUBKEY "zlinst" "1"
  WriteRegStr HKLM $PTPRO_SUBKEY "szlinst" "1"
  WriteRegStr HKLM $PTPRO_SUBKEY "bz2linst" "1"

SectionEnd

SectionGroup "PyTables Pro Requisites"
Section "Numpy @NUMPYVERSION@" NA

  # Copy Numpy package in the system directory
  CreateDirectory $PYTHONDIR\Lib\site-packages\numpy
  SetOutPath $PYTHONDIR\Lib\site-packages\numpy
  File /r @NUMPY@\*

  # Update the registry
  WriteRegStr HKLM $PTPRO_SUBKEY "numpyinst" "1"
SectionEnd

SectionGroupEnd # PyTables Pro requisites section group

# Set descriptions for Section 0 (PT section)
LangString DESC_PT $(LANG_ENGLISH) "A hierarchical database package designed \
to efficiently manage very large amounts of data."

# Set descriptions for Section 2 (NA section)
LangString DESC_NA $(LANG_ENGLISH) "The fundamental package for scientific computing \
with Python."

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT 0 $(DESC_PT)
  !insertmacro MUI_DESCRIPTION_TEXT 2 $(DESC_NA)
!insertmacro MUI_FUNCTION_DESCRIPTION_END

###########################################################
#	PART II: UNINSTALLER
###########################################################


# --------------------------------
# Pages
# --------------------------------

# Components selection page
!define MUI_PAGE_HEADER_TEXT "Choose components"
!define MUI_PAGE_HEADER_SUBTEXT "Choose which features of PyTables Pro @VERSION@ \
you want to uninstall."
!define MUI_COMPONENTSPAGE_TEXT_TOP $UNCUSTOMTEXT
!define MUI_COMPONENTSPAGE_TEXT_COMPLIST " "
!insertmacro MUI_UNPAGE_COMPONENTS

# Uninstall page: execute uninstall sections
!define MUI_INSTFILESPAGE_FINISHHEADER_TEXT "Uninstallation complete"
!define MUI_INSTFILESPAGE_FINISHHEADER_SUBTEXT "Components deletion completed \
succesfully."
#!define MUI_UNFINSIHPAGE_NOAUTOCLOSE 1
!insertmacro MUI_UNPAGE_INSTFILES

# Eventually the LANGUAGE macro must be inserted 
!insertmacro MUI_LANGUAGE "English"

# --------------------------------
# Uninstaller Sections
# --------------------------------

Section "Un.PyTables Pro @PTVERSION@ uninstall" UPT

  ReadRegStr $PYTHONDIR HKLM $PTPRO_SUBKEY "PYTHONDIR"
  ReadRegStr $INSTDIR HKLM $PTPRO_SUBKEY "INSTDIR"

  # Remove the application directory
  RMDir /r $INSTDIR

  # Remove the tables package
  RMDir /r "$PYTHONDIR\Lib\site-packages\tables"
  Delete "$PYTHONDIR\Scripts\nctoh5"
  Delete "$PYTHONDIR\Scripts\ptdump"
  Delete "$PYTHONDIR\Scripts\ptrepack"

  # Remove the application entry in the Start Menu
  !insertmacro MUI_STARTMENU_GETFOLDER Application $MUI_TEMP

  Delete "$SMPROGRAMS\$MUI_TEMP\Uninstall.lnk"
  Delete "$SMPROGRAMS\$MUI_TEMP\Examples.lnk"
  Delete "$SMPROGRAMS\$MUI_TEMP\User's Guide (PDF).lnk"
  Delete "$SMPROGRAMS\$MUI_TEMP\User's Guide (HTML).lnk"

  # Delete empty start menu parent directories
  StrCpy $MUI_TEMP "$SMPROGRAMS\$MUI_TEMP"

  startMenuDeleteLoop:
	ClearErrors
    RMDir $MUI_TEMP
    GetFullPathName $MUI_TEMP "$MUI_TEMP\.."

    IfErrors startMenuDeleteLoopDone

    StrCmp $MUI_TEMP $SMPROGRAMS startMenuDeleteLoopDone startMenuDeleteLoop
  startMenuDeleteLoopDone:

  # Delete the PyTables Pro key from the registry
  DeleteRegKey HKLM $PTPRO_SUBKEY


SectionEnd

Section "Un.Numpy @NUMPYVERSION@ uninstall" UNA

  # Remove the numpy package
  StrCmp $PYTHONDIR "" 0 +2
    ReadRegStr $PYTHONDIR HKLM $PTPRO_SUBKEY "PYTHONDIR"
  RMDir /r "$PYTHONDIR\Lib\site-packages\numpy"

  # Update the registry (if the key still exists)
  ReadRegStr $0 HKLM $PTPRO_SUBKEY "numpyinst"
  StrCmp $0 "" +2 0
    WriteRegStr HKLM $PTPRO_SUBKEY "numpyinst" "0"

SectionEnd


# --------------------------------
# Functions
# --------------------------------

Function un.onInit
  # Variables set in installer functions are not available here so we must set
  # PTPRO_SUBKEY again
  StrCpy $PTPRO_SUBKEY "SOFTWARE\Carabos\PyTables_Pro\Python@PYTHONVERSION@"
  StrCpy $UNCUSTOMTEXT "Check the components you want to uninstall. Beware that\
   ,if PyTables Pro is uninstalled, this uninstaller will not be longer \
   available."
  # The SF_RO variable is defined in Sections.nsh. Its current value
  # is 16 what means Read-only section
  ReadRegStr $0 HKLM $PTPRO_SUBKEY "numpyinst"
  StrCmp $0 "1" new1 0
    SectionSetFlags ${UNA} ${SF_RO}
  new1:
FunctionEnd


