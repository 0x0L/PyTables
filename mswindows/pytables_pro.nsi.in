# Use Modern User Interface style
!include "MUI2.nsh"
!include "nsDialogs.nsh"
!include "Library.nsh"
!include "Sections.nsh"
!include "LogicLib.nsh"
!include "WordFunc.nsh"


# Select the compressor that will compress the installer
#SetCompressor bzip2
SetCompressor /SOLID lzma
#SetCompressor /SOLID zlib
#SetCompress off

# The name of the installer
Name "PyTables Pro @VERSION@"

# The file where the installer will be saved
OutFile "PyTablesPro-@VERSION@-@LICENSE@.win32-py@DOTPYTHONVERSION@.exe"

# The commands Icon/UninstallIcon with MUI Instead we have to use
# the following !define (see NSIS FAQ)
!define MUI_ICON "@SVN_PTPRO@\mswindows\ptinstaller.ico"
!define MUI_UNICON "@SVN_PTPRO@\mswindows\ptinstaller.ico"

# Use the new XP style
XPStyle on

# Variables declaration
Var PTPRO_KEY
Var WINPTPRO_KEY
Var PTPRO_SUBKEY
Var WINPTPRO_SUBKEY
Var STARTMENU_FOLDER
Var MUI_TEMP
Var PYTHONDIR
Var UNCUSTOMTEXT

###########################################################
#	PART I: INSTALLER
###########################################################

# --------------------------------
# Functions
# --------------------------------

!ifdef VER_MAJOR & VER_MINOR & VER_REVISION

  !insertmacro VersionCompare

!endif

Function .onInit
  # Force the use of the 'all users' shell folder 
  SetShellVarContext all
  # The registry key where application settings will be stored
  StrCpy $PTPRO_KEY "SOFTWARE\PyTablesPro"
  StrCpy $WINPTPRO_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\PyTablesPro"
  StrCpy $PTPRO_SUBKEY "$PTPRO_KEY-py@DOTPYTHONVERSION@"
  StrCpy $WINPTPRO_SUBKEY "$WINPTPRO_KEY-py@DOTPYTHONVERSION@"
  StrCpy $STARTMENU_FOLDER "PyTables Pro @VERSION@ for Python@DOTPYTHONVERSION@"
FunctionEnd

Function preInstallDir
  # Default installation directory
  StrCpy $INSTDIR "$PROGRAMFILES\PyTables\$STARTMENU_FOLDER"
FunctionEnd

Function prePythonDir
  # Default Python interpreter
  StrCpy $PYTHONDIR "C:\python@PYTHONVERSION@"
FunctionEnd

Function postPythonDir
  IfFileExists $PYTHONDIR\pythonw.exe +3 0
  MessageBox MB_YESNO "Cannot find a valid Python@DOTPYTHONVERSION@ interpreter in \
$PYTHONDIR.$\r$\nYou can install PyTables Pro there, but it will not run \
without Python@DOTPYTHONVERSION@ installed in the same directory tree.$\r$\nContinue \
anyway?" IDYES +2 IDNO 0
  Abort
FunctionEnd

# --------------------------------
# Pages
# --------------------------------

# Welcome page
!insertmacro MUI_PAGE_WELCOME

# Deactivate the summary, as I don't think it is really necessary
# License summary page
#!define MUI_PAGE_HEADER_TEXT "License Summary"
#!define MUI_PAGE_HEADER_SUBTEXT "Just for lazy people :-)"
#!define MUI_LICENSEPAGE_TEXT_TOP " "
#!define MUI_LICENSEPAGE_TEXT_BOTTOM " "
#!define MUI_LICENSEPAGE_BUTTON "I'm warned"
#!insertmacro MUI_PAGE_LICENSE @SVN_PTPRO@\LICENSE_SUMMARY.txt

# License page
!insertmacro MUI_PAGE_LICENSE @SVN_PTPRO@\@LICENSEFILENAME@

# The check for an already installed package
!ifdef VER_MAJOR & VER_MINOR & VER_REVISION
Page custom PageReinstall PageLeaveReinstall
!endif

# Components selection page
!define MUI_COMPONENTSPAGE_TEXT_COMPLIST "Do not forget to check the NumPy \
and Numexpr components if they are not already installed in a standard \
location of your system."
!insertmacro MUI_PAGE_COMPONENTS

# Installation directory page for PyTables Pro data files
!define MUI_PAGE_CUSTOMFUNCTION_PRE preInstallDir
!define MUI_DIRECTORYPAGE_TEXT_TOP "Setup will install PyTables Pro @VERSION@ data \
files in the following folder. To install in a different folder, click \
Browse and select another folder. Click Next to Continue."
!insertmacro MUI_PAGE_DIRECTORY

# Installation directory page for PyTables Pro modules and scripts
!define MUI_PAGE_CUSTOMFUNCTION_PRE prePythonDir
!define MUI_PAGE_CUSTOMFUNCTION_LEAVE postPythonDir
!define MUI_PAGE_HEADER_TEXT "Choose the Python-@DOTPYTHONVERSION@ interpreter"
!define MUI_PAGE_HEADER_SUBTEXT "Choose the folder in which the Python-@DOTPYTHONVERSION@ \
interpreter is located."
!define MUI_DIRECTORYPAGE_TEXT_TOP "Setup will install PyTables Pro @VERSION@ modules and \
scripts under the following folder. To install in a different folder, click \
Browse and select another folder. Click Next to Continue."
!define MUI_DIRECTORYPAGE_VARIABLE $PYTHONDIR
!insertmacro MUI_PAGE_DIRECTORY


# Start Menu Folder Page Configuration
!define MUI_STARTMENUPAGE_REGISTRY_ROOT "HKLM"
!define MUI_STARTMENUPAGE_REGISTRY_KEY $PTPRO_SUBKEY
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "Start Menu Folder"
!insertmacro MUI_PAGE_STARTMENU Application $STARTMENU_FOLDER

# Install selected components page
!define MUI_FINISHPAGE_CANCEL_ENABLED
!insertmacro MUI_PAGE_INSTFILES

# The stuff to install a.k.a. Sections
# ------------------------------------------

Section "PyTables Pro @PTVERSION@" PT

  # Copy the PyTables Pro utilities in the Python Scripts directory
  SetOutPath $PYTHONDIR\Scripts
  File /x .svn @SVN_PTPRO@\utils\*

  # Copy the PyTables Pro sources in the Python site-packages directory
  SetOutPath $PYTHONDIR\Lib\site-packages\tables
  #File /r /x .svn @SVN_PTPRO@\tables\*
  # It is better to copy the files directly from installed directory
  File /r @TABLES_ORIG@\*

  # Copy the PyTables Pro extensions in the tables directory
  #SetOutPath $PYTHONDIR\Lib\site-packages\tables
  #File @TABLES_ORIG@\*.pyd
  #SetOutPath $PYTHONDIR\Lib\site-packages\tables\numexpr
  #File @TABLES_ORIG@\numexpr\*.pyd

  # The text files: TODO.txt, INSTALL.txt and stuff like that
  SetOutPath $INSTDIR
  File /x packagingHOWTO.txt /x LICENSE-* @SVN_PTPRO@\*.txt
  File @SVN_PTPRO@\@LICENSEFILENAME@
  Rename @LICENSEFILENAME@ LICENSE.txt

  # The documentation directory
  SetOutPath $INSTDIR\doc
  File @SVN_PTPRO@\doc\usersguide.pdf
  SetOutPath $INSTDIR\doc\html
  File /r @SVN_PTPRO@\doc\html\*

  # The examples directory
  SetOutPath $INSTDIR\examples
  File @SVN_PTPRO@\examples\*.py
  File @SVN_PTPRO@\examples\check_examples.sh

  # The LICENSES directory
  SetOutPath $INSTDIR\LICENSES
  File /r /x .svn @SVN_PTPRO@\LICENSES\*.txt

  # Create the uninstaller and put data in the aplication directory
  SetOutPath $INSTDIR
  WriteUninstaller "$INSTDIR\Uninstall.exe"

  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application

    # Create shortcuts
    CreateDirectory "$SMPROGRAMS\$STARTMENU_FOLDER"
    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\Examples.lnk" "$INSTDIR\examples"
    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\User's Guide (PDF).lnk" "$INSTDIR\doc\usersguide.pdf"
    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\User's Guide (HTML).lnk" "$INSTDIR\doc\html\index.html"
    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\Uninstall.lnk" "$INSTDIR\Uninstall.exe"

  !insertmacro MUI_STARTMENU_WRITE_END

  # Install needed libraries under the PyTables Pro directory
  SetOutPath $PYTHONDIR\Lib\site-packages\tables
  !insertmacro InstallLib DLL NOTSHARED NOREBOOT_PROTECTED @TABLES_ORIG@\hdf5dll.dll \
  hdf5dll.dll $PYTHONDIR\Lib\site-packages\tables
  !insertmacro InstallLib DLL NOTSHARED NOREBOOT_PROTECTED @TABLES_ORIG@\zlib1.dll \
  zlib1.dll $PYTHONDIR\Lib\site-packages\tables
  !insertmacro InstallLib DLL NOTSHARED NOREBOOT_PROTECTED @TABLES_ORIG@\szlibdll.dll \
  szlibdll.dll $PYTHONDIR\Lib\site-packages\tables
  !insertmacro InstallLib DLL NOTSHARED NOREBOOT_PROTECTED @TABLES_ORIG@\bzip2.dll \
  bzip2.dll $PYTHONDIR\Lib\site-packages\tables

  # Update the registry
  WriteRegStr HKLM $PTPRO_SUBKEY "" $INSTDIR
  WriteRegStr HKLM $PTPRO_SUBKEY "PYTHONDIR" $PYTHONDIR
  WriteRegStr HKLM $PTPRO_SUBKEY "hdfinst" "1"
  WriteRegStr HKLM $PTPRO_SUBKEY "zlinst" "1"
  WriteRegStr HKLM $PTPRO_SUBKEY "szlinst" "1"
  WriteRegStr HKLM $PTPRO_SUBKEY "bz2linst" "1"
!ifdef VER_MAJOR & VER_MINOR & VER_REVISION
  WriteRegDword HKLM $PTPRO_SUBKEY "VersionMajor" "${VER_MAJOR}"
  WriteRegDword HKLM $PTPRO_SUBKEY "VersionMinor" "${VER_MINOR}"
  WriteRegDword HKLM $PTPRO_SUBKEY "VersionRevision" "${VER_REVISION}"
!endif

  WriteRegExpandStr HKLM $WINPTPRO_SUBKEY "UninstallString" '"$INSTDIR\Uninstall.exe"'
  WriteRegExpandStr HKLM $WINPTPRO_SUBKEY "InstallLocation" "$INSTDIR"
  WriteRegStr HKLM $WINPTPRO_SUBKEY "DisplayName" "PyTables Pro @VERSION@ for Python@DOTPYTHONVERSION@"
  WriteRegStr HKLM $WINPTPRO_SUBKEY "DisplayIcon" "$INSTDIR\Uninstall.exe,0"
  WriteRegStr HKLM $WINPTPRO_SUBKEY "DisplayVersion" "@VERSION@"
!ifdef VER_MAJOR & VER_MINOR & VER_REVISION
  WriteRegDWORD HKLM $WINPTPRO_SUBKEY "VersionMajor" "${VER_MAJOR}"
  WriteRegDWORD HKLM $WINPTPRO_SUBKEY "VersionMinor" "${VER_MINOR}.${VER_REVISION}"
!endif
  WriteRegStr HKLM $WINPTPRO_SUBKEY "URLInfoAbout" "http://www.pytables.com/"
  WriteRegStr HKLM $WINPTPRO_SUBKEY "HelpLink" "http://www.pytables.com/moin/ProfessionalSupport"
  WriteRegDWORD HKLM $WINPTPRO_SUBKEY "NoModify" "1"
  WriteRegDWORD HKLM $WINPTPRO_SUBKEY "NoRepair" "1"


SectionEnd

SectionGroup /e "PyTables Pro Requisites"
Section /o "NumPy @NUMPYVERSION@" NP

  # Copy Numpy package in the system directory
  CreateDirectory $PYTHONDIR\Lib\site-packages\numpy
  SetOutPath $PYTHONDIR\Lib\site-packages\numpy
  File /r @NUMPY@\*

  # Update the registry
  WriteRegStr HKLM $PTPRO_SUBKEY "numpyinst" "1"

SectionEnd

Section /o "Numexpr @NUMEXPRVERSION@" NE

  # Copy Numexpr package in the system directory
  CreateDirectory $PYTHONDIR\Lib\site-packages\numexpr
  SetOutPath $PYTHONDIR\Lib\site-packages\numexpr
  File /r @NUMEXPR@\*

  # Update the registry
  WriteRegStr HKLM $PTPRO_SUBKEY "numexprinst" "1"

SectionEnd

SectionGroupEnd # PyTables Pro requisites section group

# Set descriptions for Section 0 (PT section)
LangString DESC_PT $(LANG_ENGLISH) "A hierarchical database package designed \
to efficiently manage very large amounts of data."

# Set descriptions for Section 2 (NP section)
LangString DESC_NP $(LANG_ENGLISH) "The fundamental package for scientific computing \
with Python."

# Set descriptions for Section 3 (NE section)
LangString DESC_NE $(LANG_ENGLISH) "Fast numerical array expression evaluator \
for NumPy."

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT 0 $(DESC_PT)
  !insertmacro MUI_DESCRIPTION_TEXT 2 $(DESC_NP)
  !insertmacro MUI_DESCRIPTION_TEXT 3 $(DESC_NE)
!insertmacro MUI_FUNCTION_DESCRIPTION_END

;--------------------------------
;Installer Functions

!ifdef VER_MAJOR & VER_MINOR & VER_REVISION

Var ReinstallPageCheck

Function PageReinstall

  ReadRegStr $R0 HKLM $PTPRO_SUBKEY ""

  ${If} $R0 == ""
    Abort
  ${EndIf}

  ReadRegDWORD $R0 HKLM $PTPRO_SUBKEY "VersionMajor"
  ReadRegDWORD $R1 HKLM $PTPRO_SUBKEY "VersionMinor"
  ReadRegDWORD $R2 HKLM $PTPRO_SUBKEY "VersionRevision"
  StrCpy $R0 $R0.$R1.$R2

  ${VersionCompare} ${VER_MAJOR}.${VER_MINOR}.${VER_REVISION} $R0 $R0
  ${If} $R0 != 2
    StrCpy $R1 "An older version of PyTables Pro is installed on your system. It's recommended that you uninstall the current version before installing. Select the operation you want to perform and click Next to continue."
    StrCpy $R2 "Uninstall before installing"
    StrCpy $R3 "Do not uninstall"
    !insertmacro MUI_HEADER_TEXT "Already Installed" "Choose how you want to install PyTables Pro."
    StrCpy $R0 "1"
  ${Else}
    StrCpy $R1 "A newer version of PyTables Pro is already installed! It is not recommended that you install an older version. If you really want to install this older version, it's better to uninstall the current version first. Select the operation you want to perform and click Next to continue."
    StrCpy $R2 "Uninstall before installing"
    StrCpy $R3 "Do not uninstall"
    !insertmacro MUI_HEADER_TEXT "Already Installed" "Choose how you want to install PyTables Pro."
    StrCpy $R0 "1"
  ${EndIf}

  nsDialogs::Create /NOUNLOAD 1018
  Pop $R4

  ${NSD_CreateLabel} 0 0 100% 24u $R1
  Pop $R1

  ${NSD_CreateRadioButton} 30u 50u -30u 8u $R2
  Pop $R2
  ${NSD_OnClick} $R2 PageReinstallUpdateSelection

  ${NSD_CreateRadioButton} 30u 70u -30u 8u $R3
  Pop $R3
  ${NSD_OnClick} $R3 PageReinstallUpdateSelection

  ${If} $ReinstallPageCheck != 2
    SendMessage $R2 ${BM_SETCHECK} ${BST_CHECKED} 0
  ${Else}
    SendMessage $R3 ${BM_SETCHECK} ${BST_CHECKED} 0
  ${EndIf}

  nsDialogs::Show

FunctionEnd

Function PageReinstallUpdateSelection

  Pop $R1

  ${NSD_GetState} $R2 $R1

  ${If} $R1 == ${BST_CHECKED}
    StrCpy $ReinstallPageCheck 1
  ${Else}
    StrCpy $ReinstallPageCheck 2
  ${EndIf}

FunctionEnd

Function PageLeaveReinstall

  ${NSD_GetState} $R2 $R1

  StrCmp $R0 "1" 0 +2
    StrCmp $R1 "1" reinst_uninstall reinst_done

  StrCmp $R0 "2" 0 +3
    StrCmp $R1 "1" reinst_done reinst_uninstall

  reinst_uninstall:
  ReadRegStr $R1 HKLM $WINPTPRO_SUBKEY "UninstallString"

  ;Run uninstaller
  HideWindow

    ClearErrors
    ExecWait '$R1'

    IfErrors no_remove_uninstaller
    IfFileExists "$INSTDIR\README.txt" no_remove_uninstaller

      Delete $R1
      RMDir $INSTDIR

    no_remove_uninstaller:

  StrCmp $R0 "2" 0 +2
    Quit

  BringToFront

  reinst_done:

FunctionEnd

!endif # VER_MAJOR & VER_MINOR & VER_REVISION


###########################################################
#	PART II: UNINSTALLER
###########################################################


# --------------------------------
# Pages
# --------------------------------

# Components selection page
!define MUI_PAGE_HEADER_TEXT "Choose components"
!define MUI_PAGE_HEADER_SUBTEXT "Choose which features of PyTables Pro @VERSION@ \
you want to uninstall."
!define MUI_COMPONENTSPAGE_TEXT_TOP $UNCUSTOMTEXT
!define MUI_COMPONENTSPAGE_TEXT_COMPLIST " "
!insertmacro MUI_UNPAGE_COMPONENTS

# Uninstall page: execute uninstall sections
!define MUI_INSTFILESPAGE_FINISHHEADER_TEXT "Uninstallation complete"
!define MUI_INSTFILESPAGE_FINISHHEADER_SUBTEXT "Components deletion completed \
succesfully."
#!define MUI_UNFINSIHPAGE_NOAUTOCLOSE 1
!insertmacro MUI_UNPAGE_INSTFILES

# Eventually the LANGUAGE macro must be inserted 
!insertmacro MUI_LANGUAGE "English"

# --------------------------------
# Uninstaller Sections
# --------------------------------

Section "Un.PyTables Pro @PTVERSION@ uninstall" UPT

  # Force the use of the 'all users' shell folder 
  SetShellVarContext all

  ReadRegStr $PYTHONDIR HKLM $PTPRO_SUBKEY "PYTHONDIR"
  ReadRegStr $INSTDIR HKLM $PTPRO_SUBKEY ""

  # Remove the application directory
  RMDir /r $INSTDIR

  # Remove the tables package
  RMDir /r "$PYTHONDIR\Lib\site-packages\tables"
  Delete "$PYTHONDIR\Scripts\nctoh5"
  Delete "$PYTHONDIR\Scripts\ptdump"
  Delete "$PYTHONDIR\Scripts\ptrepack"

  # Remove the application entry in the Start Menu
  !insertmacro MUI_STARTMENU_GETFOLDER Application $MUI_TEMP

  Delete "$SMPROGRAMS\$MUI_TEMP\Uninstall.lnk"
  Delete "$SMPROGRAMS\$MUI_TEMP\Examples.lnk"
  Delete "$SMPROGRAMS\$MUI_TEMP\User's Guide (PDF).lnk"
  Delete "$SMPROGRAMS\$MUI_TEMP\User's Guide (HTML).lnk"

  # Delete empty start menu parent directories
  StrCpy $MUI_TEMP "$SMPROGRAMS\$MUI_TEMP"

  startMenuDeleteLoop:
	ClearErrors
    RMDir $MUI_TEMP
    GetFullPathName $MUI_TEMP "$MUI_TEMP\.."

    IfErrors startMenuDeleteLoopDone

    StrCmp $MUI_TEMP $SMPROGRAMS startMenuDeleteLoopDone startMenuDeleteLoop
  startMenuDeleteLoopDone:

  # Delete the PyTables Pro key from the registry
  DeleteRegKey HKLM $PTPRO_SUBKEY


SectionEnd


Section /o "Un.NumPy @NUMPYVERSION@ uninstall" UNP

  # Remove the numpy package
  StrCmp $PYTHONDIR "" 0 +2
    ReadRegStr $PYTHONDIR HKLM $PTPRO_SUBKEY "PYTHONDIR"
  RMDir /r "$PYTHONDIR\Lib\site-packages\numpy"

  # Update the registry (if the key still exists)
  ReadRegStr $0 HKLM $PTPRO_SUBKEY "numpyinst"
  StrCmp $0 "" +2 0
    WriteRegStr HKLM $PTPRO_SUBKEY "numpyinst" "0"

SectionEnd

Section /o "Un.Numexpr @NUMEXPRVERSION@ uninstall" UNE

  # Remove the numexpr package
  StrCmp $PYTHONDIR "" 0 +2
    ReadRegStr $PYTHONDIR HKLM $PTPRO_SUBKEY "PYTHONDIR"
  RMDir /r "$PYTHONDIR\Lib\site-packages\numexpr"

  # Update the registry (if the key still exists)
  ReadRegStr $0 HKLM $PTPRO_SUBKEY "numexprinst"
  StrCmp $0 "" +2 0
    WriteRegStr HKLM $PTPRO_SUBKEY "numexprinst" "0"

SectionEnd


# --------------------------------
# Functions
# --------------------------------

Function un.onInit
  # Variables set in installer functions are not available here so we must set
  # them again
  StrCpy $PTPRO_KEY "SOFTWARE\PyTablesPro"
  StrCpy $WINPTPRO_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\PyTablesPro"
  StrCpy $PTPRO_SUBKEY "$PTPRO_KEY-py@DOTPYTHONVERSION@"
  StrCpy $WINPTPRO_SUBKEY "$WINPTPRO_KEY-py@DOTPYTHONVERSION@"
  StrCpy $UNCUSTOMTEXT "Check the components you want to uninstall.  Note that uninstalled components are not elegible for uninstalling."
  # The SF_RO variable is defined in Sections.nsh. Its current value
  # is 16 what means Read-only section
  ReadRegStr $0 HKLM $PTPRO_SUBKEY "numpyinst"
  StrCmp $0 "1" new1 0
    SectionSetFlags  ${UNP} ${SF_RO}
    SectionSetFlags  ${UNE} ${SF_RO}
  new1:
FunctionEnd


